// SPDX-License-Identifier: GPL-2.0-or-later
pragma solidity 0.8.28;

import {Nonces} from "openzeppelin5/utils/Nonces.sol";
import {Clones} from "openzeppelin5/proxy/Clones.sol";
import {IERC4626} from "openzeppelin5/interfaces/IERC4626.sol";
import {Create2Factory} from "common/utils/Create2Factory.sol";

import {ISiloVault} from "silo-vaults/contracts/interfaces/ISiloVault.sol";
import {ISiloVaultsFactory} from "silo-vaults/contracts/interfaces/ISiloVaultsFactory.sol";
import {IdleVaultsFactory} from "silo-vaults/contracts/IdleVaultsFactory.sol";
import {ISiloVaultDeployer} from "silo-vaults/contracts/interfaces/ISiloVaultDeployer.sol";
import {ISilo} from "silo-core/contracts/interfaces/ISilo.sol";
import {IShareToken} from "silo-core/contracts/interfaces/IShareToken.sol";
import {IGaugeHookReceiver} from "silo-core/contracts/interfaces/IGaugeHookReceiver.sol";


/// @title SiloVaultDeployer
contract SiloVaultDeployer is ISiloVaultDeployer, Create2Factory {
    ISiloVaultsFactory public immutable SILO_VAULTS_FACTORY;
    IdleVaultsFactory public immutable IDLE_VAULTS_FACTORY;

    /// @dev Factories initialization
    /// @param _siloVaultsFactory The factory for deploying Silo Vaults.
    /// @param _idleVaultsFactory The factory for deploying Idle Vaults.
    constructor(
        ISiloVaultsFactory _siloVaultsFactory,
        IdleVaultsFactory _idleVaultsFactory
    ) {
        require(address(_siloVaultsFactory) != address(0), EmptySiloVaultFactory());
        require(address(_idleVaultsFactory) != address(0), EmptyIdleVaultFactory());

        SILO_VAULTS_FACTORY = _siloVaultsFactory;
        IDLE_VAULTS_FACTORY = _idleVaultsFactory;
    }

    /// @inheritdoc ISiloVaultDeployer
    function createSiloVault(CreateSiloVaultParams memory params)
        external
        returns (
            ISiloVault vault,
            IERC4626 idleVault
        )
    {
        bytes32 salt = _salt();

        address predictedAddress = _predictSiloVaultAddress({
            _initialOwner: params.initialOwner,
            _initialTimelock: params.initialTimelock,
            _asset: params.asset,
            _name: params.name,
            _symbol: params.symbol,
            _externalSalt: salt
        });

        // 3. Deploy Silo Vault
        vault = _deploySiloVault({
            _params: params,
            _salt: salt
        });

        require(address(vault) == predictedAddress, VaultAddressMismatch());

        // 4. Deploy Idle Vault
        idleVault = IERC4626(address(
            IDLE_VAULTS_FACTORY.createIdleVault({_vault: IERC4626(address(vault)), _externalSalt: salt})
        ));

        emit CreateSiloVault(address(vault), address(idleVault));
    }

    /// @dev Deploys the Silo Vault.
    /// @param _params The parameters for the deployment.
    /// @param _salt The salt for the deployment.
    function _deploySiloVault(
        CreateSiloVaultParams memory _params,
        bytes32 _salt
    ) internal returns (ISiloVault vault) {
        vault = SILO_VAULTS_FACTORY.createSiloVault({
            _initialOwner: _params.initialOwner,
            _initialTimelock: _params.initialTimelock,
            _asset: _params.asset,
            _name: _params.name,
            _symbol: _params.symbol,
            _externalSalt: _salt
        });
    }

    /// @dev Predicts the address of the Silo Vault.
    /// @param _initialOwner The initial owner of the vault.
    /// @param _initialTimelock The initial timelock of the vault.
    /// @param _asset The asset of the vault.
    /// @param _name The name of the vault.
    /// @param _symbol The symbol of the vault.
    /// @param _externalSalt The salt that is generated by the Silo Vault Deployer.
    /// @return predictedAddress The address of the Silo Vault.
    function _predictSiloVaultAddress(
        address _initialOwner,
        uint256 _initialTimelock,
        address _asset,
        string memory _name,
        string memory _symbol,
        bytes32 _externalSalt
    ) internal view returns (address predictedAddress) {
        uint256 nonce = Nonces(address(SILO_VAULTS_FACTORY)).nonces(address(this));
        bytes32 salt = _siloVaultFactorySaltPreview(_externalSalt, nonce++);

        predictedAddress = SILO_VAULTS_FACTORY.predictSiloVaultAddress({
            _constructorArgs: abi.encode(
                _initialOwner,
                _initialTimelock,
                _asset,
                _name,
                _symbol
            ),
            _salt: salt,
            _deployer: address(SILO_VAULTS_FACTORY)
        });
    }

    /// @dev Generates the salt for the Silo Vault deployment.
    /// @param _externalSalt The salt that is generated by the Silo Vault Deployer.
    /// @param _nonce The nonce for the deployment.
    /// @return salt The salt that will be generated by the Silo Vault Factory.
    function _siloVaultFactorySaltPreview(bytes32 _externalSalt, uint256 _nonce) internal view returns (bytes32 salt) {
        salt = keccak256(abi.encodePacked(address(this), _nonce, _externalSalt));
    }
}
